[Sub_search_registry64_uninstall_keys]

DefStringList $TempList$
; "Unset" erforderlich, da evtl. dieses Sub mehrmals genutzt wird und alte Werte in der Liste sein könnten
Set $ResultList64$ = emptylist ($ResultList64$)

; reg query liefert alle Zeilen, in denen Searchpattern gefunden wird, auch z.B. DisplayName
Set $TempList$ = getOutStreamFromSection ("execWith_registry64_query '%SystemRoot%\cmd64.exe' /C")
; wir wollen aber nur die Zeilen haben, in denen der RegPath enthalten ist, um z.B. eine MSI-ID zu erhalten
Set $TempList$ = getListContaining($TempList$,$RegUninstall$)
; gehe alle Zeilen durch und nimm die Werte nach dem letzten Backslash
if not ( count ($TempList$) = "0" ) 
	; zusätzliche Filterung, da ein anderes Paket auch auf den Suchbegriff anspringen könnte
	for %s% in $TempList$ do Sub_filter_result64
endif

[Sub_filter_result64]
; gehe alle Zeilen durch und nimm die Werte nach dem letzten Backslash, aber nur wenn $SearchPattern$ enthalten ist oder eine MSI-ID erkannt wird
; Installer oder bei 'files_install' sollte die $ProductName$ enthalten sein
if (contains("%s%", $SearchPattern$))
	Set $ResultList64$ = addtolist($ResultList64$, takeString (-1,splitString("%s%", '\')))
endif
; eine MSI-ID wie z.B. {23170F69-40C1-2702-1512-000001000000} enthält immer diese spezifischen Zeichen: { - }
if (contains("%s%", "{") AND contains("%s%", "-") AND contains("%s%", "}"))
	if (contains(GetRegistryStringValue64("[" + $RegUninstall$ + "\" + $RegId$ + "] DisplayName"), $SearchPattern$))
		;nur MSI-IDs, die zum Produkt gehören sollen übernommen werden
		Set $ResultList64$ = addtolist($ResultList64$, takeString (-1,splitString("%s%", '\')))
	endif
endif

[execWith_registry64_query]
@echo off
reg query "$RegUninstall$" /s /f "$SearchPattern$"

